=== intro ===
CONST INTRO = "intro"
* [Timed Mode: You have a limited number of days to achieve your goal]
    ~ countdown = true
* [Sandbox Mode: No countdown, play as long as you like.]
    ~ countdown = false
-
I was walking to school when {GOSSIP} crossed the street to walk with me.
"So," she asked with a grin, "are you still going ahead with this crazy plan to fuck every girl in our grade?"
* "Not <em>every</em> girl[."]," I said, rolling my eyes.
    "Oh, my mistake," she laughed, "just the hot ones, then?"
    "It's not like that," I frowned. "You know why I'm doing this."
* "Why stop there?"[] I winked. "First high school, then the city, then the nation!"
       "Big plans," she laughed. "I respect your ambition if nothing else."
    "It's time to make a change," I agreed. "I can't keep living like I was."
* "Why, are you jealous?"[] I asked with a raised eyebrow.
    "You wish," she snorted.
    "Come on," I laughed, "You of all people should understand why I'm doing this."
* "It's not crazy[."]," I frowned. "It's ambitious. There's a difference."
    "Sure," she nodded. "Ambitious. Extremely ambitious. You might even say <em>crazy</em> ambitious."
    "That's the point," I said resolutely. "Shoot for the moon. Stop living my life in fear."
-
"Look, I get it. That car crash scared the hell out of you, and you realized life was too short to waste. As your best friend—"
"Only friend," I interjected.
"—I support you, even with this wild plan to cure your social anxiety by having sex with every girl you see."
* "Got any advice?"
* "My plan is brilliant[."], and someday you'll tell the world you knew me before I changed the entire field of psychology."
    "Or I'll tell the news I knew you before you were beaten to death by a mob of angry women."
* "Who do you think I should start with?"
* "I still think you're jealous."
    "Keep dreaming, Casinova."
-
She stopped walking as we approached the front door. "If you're really going to do this, you might start by talking to {NYMPHO}. She has a… reputation."
* "A sexy reputation?"
* "What kind of reputation?"
* "You can just say slut."
    She frowned. <>
-
"Word on the street is that she fooled around with every member of the chess club."
"Nice. Thanks for the tip."
"If you need to know anything about any other girls, come talk to me."
"Because you're the biggest gossip in school?" I teased.
"I prefer the term information broker. Good luck!"
Taking a deep breath, I followed her in.
* [Time to change my life] -> hallway
